pkgname=sqlite
pkgver=3.7.4
pkgrel=5
pkgdesc="SQLite is a C-language library that implements a small, fast, self-contained, high-reliability, full-featured, SQL database engine"
arch=('mips')
url="https://www.sqlite.org/"
license=('custom:Public-Domain')
groups=('pspdev-ports')
depends=('toolchain')
optdepends=()
makedepends=()
source=(
    "https://github.com/sqlite/sqlite/archive/refs/tags/version-3.7.4.tar.gz"
    "${pkgname}-${pkgver}-PSP.patch"
    "gcc-15.patch"
)
sha256sums=(
    "5fdbe958102f6f9dc93f3fc2f9e61a327fe8067fed227cd63ae26ed210a2774a"
    "81bb454ea6f742e463f6eae5bde59f0345144c06ae41e765b76ebf4f089ae8d3"
    "050cb02127e38a7f0622e49510e8e0525dcc75616ea8f3dcaeb057516986a13f"
)

prepare() {
    cd "${pkgname}-version-${pkgver}"
    patch -p1 < ../${pkgname}-${pkgver}-PSP.patch
    patch -p1 < ../gcc-15.patch
    sed -i 's#@prefix@#${PSPDEV}/psp#' sqlite.pc.in sqlite3.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' sqlite.pc.in sqlite3.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' sqlite.pc.in sqlite3.pc.in
    sed -i 's#@includedir@#${prefix}/include#' sqlite.pc.in sqlite3.pc.in
}

build() {
    cd "${pkgname}-version-${pkgver}"
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib -lc -lpspuser" \
    CFLAGS="-G0 -O2 -DSQLITE_OS_OTHER=1 -DSQLITE_OS_PSP=1 -I$(psp-config --pspsdk-path)/include" \
    ./configure --host=psp --prefix="/psp" --disable-readline --disable-tcl --disable-threadsafe --disable-amalgamation
    make
}

package() {
    cd "${pkgname}-version-${pkgver}"
    make DESTDIR="$pkgdir" install

    rm "${pkgdir}/psp/lib/libsqlite3.la"
}
