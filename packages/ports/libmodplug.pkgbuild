pkgname=libmodplug
pkgver=0.8.8.5
pkgrel=4
pkgdesc="libmodplug - the library which was part of the Modplug-xmms project"
arch=('mips')
url="http://modplug-xmms.sf.net/"
license=('custom:Public-Domain')
groups=('pspdev-ports')
depends=('toolchain')
makedepends=()
optdepends=()
provides=()
source=(
"git+https://github.com/Konstanty/libmodplug#commit=d7ba5efd5816696fba668a23194940f796d62b95"
)
sha256sums=(
"SKIP"
)

prepare() {
    cd libmodplug
    sed -i 's#@prefix@#${PSPDEV}/psp#' libmodplug.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' libmodplug.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libmodplug.pc.in
    sed -i '/configure_file/{s/)/ @ONLY)/;}' CMakeLists.txt
}

build() {
    cd libmodplug
    mkdir pspbuild
    cd pspbuild
    psp-cmake .. -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd libmodplug
    make install -C pspbuild
    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

