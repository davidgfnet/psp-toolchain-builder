pkgname=leptonica
pkgver=1.84.1
pkgrel=4
pkgdesc="An open source C library for efficient image processing and image analysis operations"
arch=('mips')
url="http://www.leptonica.org/"
license=('BSD-2-Clause')
depends=('zlib' 'libpng' 'jpeg' 'toolchain')
groups=('pspdev-ports')
makedepends=()
optdepends=()
source=(
    "https://github.com/DanBloomberg/leptonica/releases/download/${pkgver}/$pkgname-${pkgver}.tar.gz"
    "${pkgname}-${pkgver}-PSP.patch"
)
sha256sums=(
    "2b3e1254b1cca381e77c819b59ca99774ff43530209b9aeb511e1d46588a64f6"
    "SKIP"
)

prepare() {
    cd "$pkgname-${pkgver}"

    patch -p1 < ../${pkgname}-${pkgver}-PSP.patch

    sed -i 's#@prefix@#${PSPDEV}/psp#' lept.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' lept.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' lept.pc.in
    sed -i 's#@includedir@#${prefix}/include#' lept.pc.in
}

build() {
    cd "$pkgname-${pkgver}"

    ./autogen.sh --host=psp
    ./configure --without-giflib --without-libtiff --without-libwebp --without-libwebpmux --without-libopenjpeg \
    --disable-programs --host=psp --prefix=/psp

    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-${pkgver}"
    make --quiet DESTDIR="$pkgdir" $MAKEFLAGS install

    rm "${pkgdir}/psp/lib/libleptonica.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 leptonica-license.txt "$pkgdir/psp/share/licenses/$pkgname"
}
